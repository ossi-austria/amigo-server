plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'org.asciidoctor.convert' version "1.5.9.2"
}

dependencies {
    implementation project(path: ':platform-domain', configuration: 'default')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"

    implementation "org.projectlombok:lombok"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.10"

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Rest and HATEOAS
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Security
    implementation "org.springframework.boot:spring-boot-starter-security"
    // spring data, JPA, Repositories and DB migration
    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.flywaydb:flyway-core"
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'

    //Email
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation "com.google.firebase:firebase-admin:$firebaseAdminVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$jupiterVersion"
    // DO NOT USE:   testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter"
    // DO NOT USE:   testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiter"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'mockito', module: 'mockito-core' //by both name and group
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
    testImplementation "io.mockk:mockk:${mockkVersion}"
    testImplementation "com.ninja-squad:springmockk:$springmockkVersion"
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // testcontainers
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:postgresql:$testcontainersVersion"

    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"

}

noArg {
    annotation("org.ossiaustria.platform.api.Dto")
    annotation("org.ossiaustria.platform.api.DtoRequest")
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    enabled = true
    mainClassName = 'org.ossiaustria.amigo.platform.RestApplicationKt'
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

jar {
    enabled = true
}

//task fatJar(type: Jar) {
//    manifest.from jar.manifest
//    from {
//        configurations.testRuntime.collect { it.isDirectory() ? it : zipTree(it) }
//    } {
//        exclude "META-INF/*.SF"
//        exclude "META-INF/*.DSA"
//        exclude "META-INF/*.RSA"
//    }
//    with jar
//}

task explodeJar(type: Copy) {
    dependsOn ":platform-rest:bootJar"
    mustRunAfter ":platform-rest:bootJar"
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

task prepareDocker {
    dependsOn ":platform-rest:bootJar"
    dependsOn ":platform-rest:explodeJar"
    mustRunAfter ":platform-rest:explodeJar"
    doLast {
        print("Exploded platform-rest bootJar for docker")
    }
}

